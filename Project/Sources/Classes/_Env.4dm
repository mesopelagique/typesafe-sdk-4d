// Process environment as an object map. Singleton: cs.jev._Env.me["VAR"]

singleton Class constructor
	
	var $in; $out; $err : Text
	If (Is Windows:C1573)
		SET ENVIRONMENT VARIABLE:C812("_4D_OPTION_HIDE_CONSOLE"; "true")
		LAUNCH EXTERNAL PROCESS:C811("cmd /C SET"; $in; $out; $err)
	Else 
		LAUNCH EXTERNAL PROCESS:C811("/usr/bin/env"; $in; $out; $err)
	End if 
	
	var $pos : Integer
	var $line : Text
	For each ($line; Split string:C1554($out; ((Is Windows:C1573) ? Char:C90(Carriage return:K15:38) : "")+Char:C90(Line feed:K15:40); sk ignore empty strings:K86:1))
		$pos:=Position:C15("="; $line)
		If ($pos>0)
			This:C1470[Substring:C12($line; 1; $pos-1)]:=Substring:C12($line; $pos+1)
		Else 
			This:C1470[$line]:=""
		End if 
	End for each 

// Read a trimmed environment value; "" when missing or blank.
Function read($name : Text) : Text
	var $value : Variant:=This:C1470[$name]
	If (Value type:C1509($value)#Is text:K8:3)
		return ""
	End if
	return cs:C1710._Utils.me.trim($value)
